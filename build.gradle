buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

version = '0.1.1'
group = 'com.github.machinarius'

dependencies {
    compile 'com.android.support:support-v4:21.0.2'
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

ext {
    artifactId = 'preferencefragment'
}

task androidJavadocs(type: Javadoc) {
    source = 'src/main/java'
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    //basename = project.artifactId
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    //basename = project.artifactId
    from 'src/main/java'
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

uploadArchives {
    configuration = configurations.archives

    repositories.mavenDeployer {
        pom.groupId = 'com.github.machinarius'
        pom.artifactId = 'preferencefragment'
        pom.version = '0.1.1'

        pom.project {
            name 'Android Support PreferenceFragment'
            packaging 'aar'
            description 'PreferenceFragment for versions prior to Honeycomb'
            url 'https://github.com/Machinarius/preferencefragment'

            scm {
                url 'scm:git@github.com/Machinarius/preferencefragment'
                connection 'scm:git@github.com/Machinarius/preferencefragment'
                developerConnection 'scm:git@github.com/Machinarius/preferencefragment'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'kolavar'
                    name 'kolavar'
                    email 'kolavar@unknown.com' //Couldn't find more details on these users
                }

                developer {
                    id 'androidpmmedia'
                    name 'androidpmmedia'
                    email 'kolavar@unknown.com' //Couldn't find more details on these users
                }

                developer {
                    id 'machinarius'
                    name 'Germ√°n Valencia'
                    email 'machinarius92@gmail.com'
                }
            }
        }
    }
}
